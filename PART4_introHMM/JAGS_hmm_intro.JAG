model{
# PRIORS
p ~ dbeta(pr.p[1], pr.p[2]) # prior on capture history
phi ~ dbeta(pr.phi[1], pr.phi[2]) # prior on survival
psi ~ dbeta(pr.psi[1], pr.psi[2])
tr[1,1] <- 1-psi # unborn to unborn
tr[2,1] <- psi   # unborn to alive
tr[3,1] <- 0     # (illegal)
# FROM alive (col2) to...
tr[1,2] <- 0     # (illegal)
tr[2,2] <- phi   # alive to alive
tr[3,2] <- 1-phi # alive to dead
# FROM dead (col3) to...
tr[1,3] <- 0     # (illegal)
tr[2,3] <- 0     # (illegal)
tr[3,3] <- 1
em[1,1]<-1  
em[2,1]<-0  
# state 2: alive
em[1,2]<-1-p
em[2,2]<-p  
# state 3: dead (100% no capture)
em[1,3]<-1  
em[2,3]<-0
z[1] ~ dcat(tr[,1]) # initialize state 1 
# CONDITIONAL LIKELIHOOD: at t=1
y[1] ~ dcat(em[,z[1]])
for(t in 2:T){
   z[t] ~ dcat(tr[,z[t-1]]) # z_t | z_t-1
   # CONDITIONAL LIKELIHOOD: for t>1
   y[t] ~ dcat(em[,z[t]]) # conditional capture
} # t
}
