model{
for(x in 1:n.sex){ # loop through sexes
  phi[x] ~ dbeta(pr.phi[x,1],pr.phi[x,2])
  p[x] ~ dbeta(pr.p[x,1],pr.p[x,2])
  # FROM alive to...
  tr[1,1,x] <- phi[x]   # alive
  tr[2,1,x] <- 1-phi[x] # dead
  # FROM dead to ...
  tr[1,2,x] <- 0 # alive (illegal)
  tr[2,2,x] <- 1 # dead to dead
  # HMM EMISSION MATRIX
  # state 1: alive
  em[1,1,x]<-1-p[x] # miss
  em[2,1,x]<- p[x]  # capture
  # state 2: dead 
  em[1,2,x]<- 1  # miss
  em[2,2,x]<- 0  # capture
} # sex
# latent proces
for(i in 1:n){
  # HMM LATENT STATE PROCESS: at t=1
  z[i,first[i]+1] ~ dcat(tr[,1,sex.v[i]]) # initialize state 1 (alive)
  # loop though capture periods after first capture
  for(t in (first[i]+1):(T-1)){
     z[i,t+1] ~ dcat(tr[,z[i,t],sex.v[i]]) # z_t | z_t-1
  } # t
}# i 
# conditional likelihood
for(i in 1:n){
  # loop though capture periods after first capture
  for(t in (first[i]+1):T){
     y[i,t] ~ dcat(em[,z[i,t],sex.v[i]])
  } # t
} # i
# Abundance estimate: horvitz-type estimator: n_obs 
for(t in 2:T){ # loop through time
  for(x in 1:n.sex){ # loop through sex
    for(i in 1:n){ # loop through individuals
      N_i[i,t-1,x] <- equals(sex.v[i],x)*equals(y[i,t],2)/p[sex.v[i]]
    } # i
    # tally estimate for sex x at time t (offset by -1)
    N[t-1,x] <- sum(N_i[,t-1,x]) # sum over all individuals of sex x
   } # x
} # t
}
